#!/usr/bin/perl

use strict;
use warnings;
use Mojolicious::Lite;
use Time::Local;
use CIBH::Config qw / $default_options /;
use CIBH::DS;
use CIBH::SNMP qw ( load_snmp_config );

app->secrets(['Change this if you run standalone']);
app->config(hypnotoad => {listen => ['http://*:80']});

my $opts = $default_options;
my $ds = CIBH::DS::load_ds($opts->{datastore});

helper show_charts => sub {
    my ($self, $opts) = @_;
    my $rtrs;
    my $stop = $self->param('stop') || time;
    my $span = $self->param('span');
    my $size = $self->param('size') || $opts->{graph_size} || '960,500';
    my $files = $self->every_param('file');
    if ($files->[0] =~ /,/ && scalar @$files == 1) {
        $files = [ split(/,/, $files->[0]) ];
    }

    # default to one day, single graph display
    my $graphs = [ { '' => $span || 86400 } ];

    # if they only ask for one file then give them day,week,month
    if (scalar @$files == 1 && !defined($span)) {
        $graphs = [
                { 'Day trend' => 86400 },
                { 'Week Trend' => 86400*7 },
                { 'Month Trend' => 86400*30 }
        ];
    }
    $self->stash(graphs => $graphs);

    foreach my $filepath (@$files) {
        next if ($filepath =~ m#^/|\.\.#);
        my ($rtr,$file) = ($filepath =~/^(.*)\/(.*)$/);

        next if (! -f "$opts->{config_path}/$rtr.snmp.config");
        my $config=load_snmp_config($rtr,$opts);
        my $iface = $config->{$rtr}->{$file}->{iface};
        next if ($rtrs->{$rtr}->{$iface});  # We already processed this

        my @metrics;
        no warnings 'uninitialized';
        # this depends on the router config being in order of input first,
        # then output.  We create this @metrics array here and then send it in
        # this order to /data which returns an array in this order.
        #
        # this is fragile and we should figure out a better way to do it.
        foreach my $filename (sort keys %{$config->{$rtr}}) {
            if ($config->{$rtr}->{$filename}->{iface} eq $iface) {
                push(@metrics, $filename);
            }
        }

        # if the interface doesn't exist we need to not add CPU
        next if (scalar @metrics == 0);
        foreach my $filename (keys %{$config->{$rtr}}) {
            if ($config->{$rtr}->{$filename}->{cpuave} == 5) {
                push(@metrics, $filename);
            }
        }

        $rtrs->{$rtr}->{$iface}->{metrics}=\@metrics;
        $rtrs->{$rtr}->{$iface}->{scale}=$config->{$rtr}->{$file}->{max};
        $rtrs->{$rtr}->{$iface}->{desc}=$config->{$rtr}->{$file}->{desc};
    }

    my ($width, $height) = split(',', $size);
    $self->stash(width => $width, height => $height);

    $self->stash(span => $span, stop => $stop, routers => $rtrs);
    return $self->render('chart');
};

helper show_chart_list => sub {
    my $self = shift;
    my $rtr = $self->param('rtr');
    $self->stash(rtr => $rtr);
    my $config=load_snmp_config($rtr,$opts);
    my $files = ();
    my $done;
    foreach my $file (sort (keys %{$config->{$rtr}})) {
        next if not defined $config->{$rtr}->{$file}->{iface};
        my $iface=$config->{$rtr}->{$file}->{iface};
        next if $done->{$iface};
        push(@$files, { file => "$rtr/$file", iface => $iface,
                       desc => $config->{$rtr}->{$file}->{desc},
                       addr => $config->{$rtr}->{$file}->{addr},
                       prefix => $config->{$rtr}->{$file}->{prefix}
                     });
        $done->{$iface}=1;
    }
    push(@$files, { file => "$rtr/cpu.1m", iface => 'CPU', desc => "cpu utilization on $rtr" });
    my @all = map { $_->{file} } @$files;
    push(@$files, { file => join(',',@all), iface => 'All' });

    $self->stash(files => $files);
    return $self->render('chartlist');
};


helper show_router_list => sub {
    my ($self, $opts) = @_;
    my(@hosts)=glob("$opts->{config_path}/*.snmp.config");
    $self->stash('files' => [map { my ($f)=($_=~ /$opts->{config_path}\/(.*)\.snmp\.config/); } @hosts]);
    return $self->render('rtrlist');
};

# updates the netblock to whatever the user provided in the parameters
helper update_net => sub {
    my ($self, $opts) = @_;
    my $network = $self->param('net') || $opts->{network};
    $self->stash(net => $network);
    my ($data) = ($opts->{data_path} =~ m#(.*)/\S+/snmp#);
    if (! -d "$data/$network") {
        # return error if the directory doesn't exist to prevent eval'ing strange things.
        return $self->render(template => "exception", status => 501 );
    }
    $opts->{network} = $network;
    $opts->{data_path}=eval "\"$opts->{data_path}\"";
    $opts->{config_path}=eval "\"$opts->{config_path}\"" ;
};

get '/' => sub {
    my $self = shift;

    $self->update_net($opts);
    $self->res->headers->expires('now');

    return $self->show_charts($opts) if ($self->param('rtr') && $self->param('file'));
    # chartlist, for when a router is specified but no chart
    return $self->show_chart_list() if ($self->param('rtr'));
    return $self->show_router_list($opts);
};

get '/data' => sub {
    my $self=shift;
    $self->update_net($opts);
    my $stop = $self->param('stop') || time;
    my $span = $self->param('span') || 86400;
    my $host = $self->param('host');
    my $metrics = $self->every_param('metric');
    my $output;
    foreach my $metric (@$metrics) {
        next if ($metric =~ m#^/|\.\.#);

        my $values = $ds->new(opts=>$opts, host=>$host, metric=>$metric)->GetValues($stop-$span, $stop);
        push(@{$output->{values}}, $values);
        push(@{$output->{files}}, "$host/$metric");
    }

    return $self->render(json => $output);
};

app->start;

__DATA__
@@ rtrlist.html.ep
<!doctype html>
<html>
<head>
<title>Available Routers</title>
</head>
<style>
div#columns {
    padding: 0px 10% 0px 10%;
}

div#columns ul {
    -webkit-columns: 120px 10;
       -moz-columns: 120px 10;
            columns: 120px 10;
         list-style: none;
             margin: 0;
            padding: 0;
}
</style>
<body>
<center><h1>Available Routers</h1></center>
<div id="columns">
<ul>
% foreach my $file (@$files) {
<li><%= link_to url_for->query(rtr => $file )->to_abs => begin %><%= $file %><% end %></li>
% }
</ul>
</div>
</body>
</html>
@@ chartlist.html.ep
<!doctype html>
<html>
<head>
<title>Available Charts</title>
</head>
<body>
<style>
td#header {
    background-color: #99cccc;
    text-align: center;
    font-size: 1.5em;
    margin: .75em 0;
    padding: 1em 0 1em 0;
    font-weight: bold;
}

td {
    background-color: #cccccc;
}

th {
    background-color: #99cccc;
}
</style>
<table>
<tr><td id="header" colspan=4>Charts available on <%= $rtr %></td></tr>
<tr><th>Name</th><th>Description</th><th>Address</th><th>Network</th></tr>
% foreach my $file (@$files) {
<tr><td>
<%= link_to url_for->query(rtr => $rtr, file => $file->{file} )->to_abs => begin %><%= $file->{iface} %><% end %></td>
<td><%= $file->{desc} %></td>
<td><%= $file->{addr} %></td>
<td><%= $file->{prefix} %></td>
</tr>
% }

</table>
</body>
</html>
@@ graph.js
function guid(){
    var d = new Date().getTime();
    var uuid = 'xxxxxxxx-xxxx-4xxx-yxxx-xxxxxxxxxxxx'.replace(/[xy]/g, function(c) {
        var r = (d + Math.random()*16)%16 | 0;
        d = Math.floor(d/16);
        return (c=='x' ? r : (r&0x3|0x8)).toString(16);
    });
    return uuid;
};

// we need a function that will handle the drawing of the graph, so that
// rather than putting lots of logic in the brushmove stuff we just call the
// same function that does the initial drawing.

// this might be an abstractions of d3.json below..
function draw(data, extents, whatever) {

};

// thought we could break this out as well.
function legend() {

};

function graph(element, legend, datapath, scale, gwidth, gheight) {

    gwidth = gwidth || 960;
    gheight = gheight || 500;
    // scale = scale || d3.max(values) + 5

    var colors = ['steelblue', 'green', 'red'];
    var scales = function(d) { return d != 2 ? line : lineRight };
    var domains = [];
    var uuid = guid();


    var margin = {top: 20, right: 40, bottom: 30, left: 50},
//    var margin = {top: 20, right: 20, bottom: 60, left: 40},
        width = gwidth - margin.left - margin.right,
        height = gheight - margin.top - margin.bottom;

    var x = d3.time.scale()
        .range([0, width]);

    var y = d3.scale.linear()
        .range([height,0]);

    var y1 = d3.scale.linear()
        .range([height,0]);

    var xAxis = d3.svg.axis()
        .scale(x)
        .orient("bottom");

    var yAxis = d3.svg.axis()
        .scale(y)
        .tickFormat(d3.format(' s'))    // converts yAxis scale to SI units (10M instead of 10,000,000)
        .orient("left");

    var yAxisRight = d3.svg.axis()
        .scale(y1)
        .orient("right");

    var lineRight = d3.svg.line()
        .x(function(d) { return x(new Date(d[0]*1000)); })
        .y(function(d) { return y1(d[1]); });

    var line = d3.svg.line()
        .x(function(d) { return x(new Date(d[0]*1000)); })
        .y(function(d) { return y(d[1]); });

    var area = d3.svg.area()
        .x(function(d) { return x(new Date(d[0]*1000)); })
        .y0(height)
        .y1(function(d) { return y(d[1]); });

    var svg = d3.select(element).append("svg")
        .attr("width", width + margin.left + margin.right)
        .attr("height", height + margin.top + margin.bottom)
      .append("g")
        .attr("transform", "translate(" + margin.left + "," + margin.top + ")");


    d3.json(datapath, function(error, data) {
      x.domain(d3.extent(data['values'][0], function(d) { return new Date(d[0]*1000); }));
      y.domain([0, scale ]);
      y1.domain([0, 100 ]);

      var brush = d3.svg.brush()
        .x(x)
        .on("brush", brushmove)
        .on("brushend", brushend);

      function brushmove() {
        var extent = brush.extent();
        graphs.classed("selected", function(d) {
            is_brushed = Date(extent[0]) <= Date(d[0][0]*1000) && Date(d[d.length - 1][0]*1000) <= Date(extent[1]);
            return is_brushed;
        });
      }

      function brushend() {
        get_button = d3.select(".clear-button" + uuid);
        if(get_button.empty() === true) {
            clear_button = svg.append('text')
              .attr("transform", "translate(" + (width - 50) + " ," + (height + margin.bottom) + ")")
              .attr("class", "clear-button" + uuid)
              .text("<- Go Back");
        }
        domains.push(x.domain());
        x.domain(brush.extent());

        transition_data();
        reset_axis();

        graphs.classed("selected", false);
        d3.select(".brush" + uuid).call(brush.clear());

        clear_button.on('click', function(){
            x.domain(domains.pop());
            transition_data();
            reset_axis();
            if (domains.length == 0) {
                clear_button.remove();
            }
        });
      }

      function transition_data() {
        svg.selectAll("path")
           .data(data['values'])
        .transition()
           .duration(500)
           .attr("class", "line")
           .attr("d", function(d,i) { var scale=scales(i); return scale(d); })
           .style("stroke", function(d,i) { return colors[i]; });
      }

      function reset_axis() {
        svg.transition().duration(500)
           .select(".x.axis")
           .call(xAxis);
      }

      svg.append("g")
          .attr("class", "x axis")
          .attr("clip-path", "url(#clip)")
          .attr("transform", "translate(0," + height + ")")
          .call(xAxis);

      svg.append("g")
          .attr("class", "y axis")
          .call(yAxis);

      svg.append("g")
          .attr("class", "y axis")
          .attr("transform", "translate(" + width + " ,0)")
          .style("fill", "red")
          .call(yAxisRight);

      svg.append("g")
        .attr("class", "grid")
        .call(d3.svg.axis()
            .scale(y1)
            .orient("left")
            .ticks(10)
            .tickSize(-width, 0, 0)
            .tickFormat("")
        );

      svg.append("g")
            .attr("class", "brush" + uuid + " brush")
            .call(brush)
         .selectAll('rect')
                .attr('height', height);

      svg.append("defs").append("clipPath")
            .attr("id", "clip")
         .append("rect")
            .attr("width", width)
            .attr("height", height + 20);

      svg.append("path")
          .datum(data['values'][0]) // The first graph values are used for area
          .attr("class", "area")
          .attr("d", area);

      graphs = svg.selectAll(".graphs")
            .data(data['values'])
            .enter().append("g")
            .append("path")
            .attr("class", "line")
            .attr("clip-path", "url(#clip)")
            .attr("d", function(d,i) { var scale=scales(i); return scale(d); })
            .style("stroke", function(d,i) { return colors[i]; });

      graphs.on('mousedown', function(){
          brush_elm = svg.select(".brush" + uuid).node();
          new_click_event = new Event('mousedown');
          new_click_event.pageX = d3.event.pageX;
          new_click_event.clientX = d3.event.clientX;
          new_click_event.pageY = d3.event.pageY;
          new_click_event.clientY = d3.event.clientY;
          brush_elm.dispatchEvent(new_click_event);
      });

      svg.append("g")
        .attr('class','date_line')
        .attr("transform", "translate(0," + height + ")")
        .call(d3.svg.axis()
            .scale(x)
            .orient("bottom")
            .ticks(1)
            .tickSize(-height, 0, 0)
            .tickFormat("")
        );


      legend_output = "<table class='graph_legend' style='width:"+ gwidth +"px;'><tr><th width=\"75%\"></th><th>Max</th><th>Ave</th><th>Cur</th></tr>";
      data['files'].forEach(function(d, i) {
          var si_format = d3.format('.3s');
          var last = data['values'][i];
          var cur = si_format(last[last.length - 1][1]);
          var max = si_format(d3.max(data['values'][i], function(d) { return d[1]; }));
          var avg = si_format(d3.mean(data['values'][i], function(d) { return d[1]; }));
          var color = colors[i];
          legend_output = legend_output + "<tr style=\"color:" + color + "\"><td>" + d + "</td><td>"+max+
                                          "</td><td>"+avg+"</td><td>"+cur+"</td></tr>";
      });
      legend_output = legend_output + "</table>";
      document.getElementsByClassName(legend)[0].innerHTML = legend_output;
    });
}
@@ chart.html.ep
<!DOCTYPE html>
<head>
<meta charset="utf-8">
<title>multiple files</title>
%= stylesheet '/graph.css'
</head>
%= javascript 'https://cdnjs.cloudflare.com/ajax/libs/d3/3.5.5/d3.min.js'
<script>window.d3 || document.write( '<script src="/js/d3.v3.js" >\x3c/script>' );</script>
%= javascript '/graph.js'

% my $graph = begin
    % my ($rtr,$metric,$stop,$span,$graph_count,$scale) = @_;
    <div class="graph_timespan"><h2><%= localtime($stop-$span) %> --- <%= localtime($stop) %><br></h2></div>

    <div class="graph_svg graph_count_<%= $graph_count %>"></div>
    <div class="graph_legend_<%= $graph_count %>"></div>

    <script> graph('.graph_count_<%= $graph_count %>', 'graph_legend_<%= $graph_count %>', '<%== url_for('/data')->query('net' => $net, 'host'=>$rtr, 'metric'=>$metric, stop => $stop, span => $span); %>', <%= $scale %>, <%= $width %>, <%= $height %>); </script>
% end

%my $graph_count=0;
% foreach my $rtr (sort keys %$routers) {
    % foreach my $iface (keys %{$routers->{$rtr}}) {
        <div class="graph_title">
        <%= link_to url_for->query(net => $net, file => "$rtr/$iface.in") => begin %><h1>Interface Utilization Chart<br><%= $rtr %><br></h1><% end %>
        </div>
        <div class="graph_desc"><h2><%= $iface %><br><%= $routers->{$rtr}->{$iface}->{desc} %></h2></div><br><br>

        % if (!defined($span) && scalar keys %$routers == 1) {
            %   for (@$graphs) {
            %      my ($header, $span) = each %$_;
                   <div class="graph_period"><h2><%= $header %></h2></div><br>
            %=     $graph->($rtr, $routers->{$rtr}->{$iface}->{metrics}, $stop, $span, $graph_count++, %$routers->{$rtr}->{$iface}->{scale});
            % }
        % } else {
        %=    $graph->($rtr, $routers->{$rtr}->{$iface}->{metrics}, $stop, $span, $graph_count++, $routers->{$rtr}->{$iface}->{scale});
        % }
    % }
% }

@@ graph.css

body {
  font: 12px times;
}

.area {
    fill: lightsteelblue;
    stroke-width: 0;
}

.axis path,
.axis line {
  fill: none;
  stroke: #000;
  shape-rendering: crispEdges;
}

.x.axis path {
  display: none;
}

.line {
    fill: none;
    stroke-width: 1.5px;
}

.date_line .tick {
  stroke: black;
  stroke-opacity: 0.7;
  shape-rendering: crispEdges;
}
.date_line path {
    stroke-width: 0;
}

.grid .tick {
  stroke: lightgrey;
  stroke-opacity: 0.7;
  shape-rendering: crispEdges;
}
.grid path {
    stroke-width: 0;
}

.selected {
  fill: #afa2dc;
  stroke: #2f225d;
}

.brush .extent {
  stroke: #fff;
  fill-opacity: .125;
  shape-rendering: crispEdges;
}

.graph_title {
    text-align: center;
}

.graph_desc {
    text-align: center;
}

.graph_period {
    text-align: center;
}

.graph_timespan {
    text-align: center;
}

.graph_svg {
    text-align: center;
}

.graph_legend {
    margin-left: auto;
    margin-right: auto;
}

.graph_legend th, td {
    text-align: left;
}

