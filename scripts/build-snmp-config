#!/usr/bin/perl

use strict;
use warnings;
use Mojo::SNMP;
use CIBH::SNMP;
use SNMP::OID::Translate qw ( translate translateObj );
use Getopt::Long;
use CIBH::Config qw/ $default_options /;

# we compile this in Main and then use it for every device.
my $oids = [];

sub Main {
    my $opts = $default_options;

    GetOptions($opts,
               "config=s",      # config file to store in
               "config_path=s",   # base directory
               "domain=s",      # domain name of router
               "rtr=s",         # router to examine
               "community=s",   # router community string
               "debug:i",       # debug messages
               "noaddress",     # don't collect addresses for aliases
               "accept=s",      # regexp for accepted interfaces
               "reject=s",      # regexp for rejected interfaces
               "datastore=s",   # Datastore type (default = Datafile)
               "noshutdown"     # don't poll shutdown interfaces?
    );

    $opts->{debug}=1 if(defined $opts->{debug} and $opts->{debug}==0);
    $opts->{domain}=".$opts->{domain}"
        if defined $opts->{domain} and substr($opts->{domain},0,1) ne '.';

    push(@ARGV, $opts->{rtr}) if (defined($opts->{rtr}));
    die "No router specified\n" if not (@ARGV);

    # only need to translate these once during setup.
    my $iftable_tags = [ 'ifDescr','ifSpeed','ifHighSpeed','ifAdminStatus', 'ifAlias' ];
    my $ipaddr_tags =  [ 'ipAddressIfIndex', 'ipAddressPrefix' ];
    $oids = [ @{translate($iftable_tags)}, @{translate($ipaddr_tags)} ];

    my $snmp = Mojo::SNMP->new;
    my $values = {};
    my $status = 0;

    # run the loop, getting SNMP info for the devices.
    foreach my $rtr (@ARGV) {
        $values->{$rtr} = { };
        $status += GetSNMPInfo($snmp,$rtr,$opts,$values);
    }

    # if some of the SNMP connections succeeded then block until they are
    # done, then write the configurations.
    if ($status) {
        $snmp->wait;

        # save the configs
        foreach my $host (keys %$values) {
            my $config = Config($host, $opts, $values->{$host});
            CIBH::Config::save_file("$opts->{config_path}/$host.snmp.config",$config,'config',$opts);
        }
    }
}

sub GetSNMPInfo {
    my($snmp,$host,$opts,$values)=(@_);
    $host .= $opts->{domain} if ($opts->{domain});

    my $oidmap = {
        # 1min                            => 5min
        '1.3.6.1.4.1.9.2.1.57.0'          => '1.3.6.1.4.1.9.2.1.58.0', # old cisco
        '1.3.6.1.4.1.9.9.109.1.1.1.1.7.1' => '1.3.6.1.4.1.9.9.109.1.1.1.1.8.1', # new cisco
    };

    # because this is called as data arrives, we can't run the Config()
    # sub at the end.  We might not have all the data yet.  We'll need to wait
    # until after $snmp->wait
    my $store = sub {
        my ($self, $err, $session) = @_;

        if($err) {
            warn "$host failed with this error: $err" if $opts->{debug};
            return;
        }
        my $results = $session->var_bind_list;

        while(my ($oid, $val) = each %{$results}) {
            next if ($val eq 'noSuchInstance' || $val eq 'noSuchObject');
            # split into only two variables even if there are more dots
            my ($tag,$idx) = split(/\./, translateObj($oid),2);
            if ($oidmap->{$oid}) {
                $values->{$host}{'cpu.1m'}=$oid;
                $values->{$host}{'cpu.5m'}=$oidmap->{$oid};
                warn "Using oid $oid and $oidmap->{$oid}\n" if($opts->{debug});
                next;
            }
            if ($tag eq 'ipAddressPrefix') {
                my ($index, $prefix) = CIBH::SNMP::parse_prefix($val);
                # only allowing ipv4 right now
                if ($prefix && $prefix =~ /^\d+\.\d+\.\d+\.\d+/) {
                    $values->{$host}{$index}{prefix}=$prefix;
                }
            } elsif ($tag eq 'ipAddressIfIndex') {
                my $addr = CIBH::SNMP::parse_ifindex($val);
                if ($addr && $addr =~ /^\d+\.\d+\.\d+\.\d+/) {
                    $values->{$host}{$idx}{addr}=$addr;
                }
            # everything else should be treated as an interface statistic
            } else {
                push(@{$values->{$host}{indexes}}, $idx) if (!$values->{$host}{$idx}{ifIndex});
                $values->{$host}{$idx}{ifIndex}=$idx;
                $values->{$host}{$idx}{$tag}=$val;
            }
        }
    };

    my $snmp_opts = { version => "v2c", community => $opts->{community} };
    my $bw_oids = [ '1.3.6.1.4.1.9.9.109.1.1.1.1.7.1',   # New Cisco
                 '1.3.6.1.4.1.9.2.1.57.0',            # Old Cisco
                 '1.3.6.1.4.1.20858.10.13.1.1.1.4',   # Casa
               ];

    # bulk_walk doesn't like looping over an array of oids.  I don't know why
    # but this is the workaround.
    for (@$oids) {
        $snmp->bulk_walk( $host, $snmp_opts, $_, $store );
    }
    $snmp->get( $host, $snmp_opts, $bw_oids, $store );
    return 1;
}

sub Config {
    my($host,$opts,$values)=(@_);

    my $config = {};
    warn "Using config generation on $host\n" if($opts->{debug});

    if (defined($values->{'cpu.1m'})) {
        $config->{$host}->{'cpu.1m'}={
            oid=> $values->{'cpu.1m'},
            command=> 'CIBH::DS::' . $opts->{datastore}->{name} . '::GaugeAppend',
            cpuave=>1,
            max=>100,
            file=>'cpu.1m',
        };
    }

    if (defined($values->{'cpu.5m'})) {
        $config->{$host}->{'cpu.5m'}={
            oid=> $values->{'cpu.5m'},
            command=> 'CIBH::DS::' . $opts->{datastore}->{name} . '::GaugeAppend',
            cpuave=>5,
            max=>100,
            file=>'cpu.5m',
        };
    }

    foreach my $index (@{$values->{indexes}}) {
        my $iface = $values->{$index}{'ifDescr'};
        next if (!$iface);

        my $speed = $values->{$index}{'ifSpeed'};
        my $highspeed = $values->{$index}{'ifHighSpeed'};
        my $stat = $values->{$index}{'ifAdminStatus'};
        my $desc = $values->{$index}{'ifAlias'};
        my $addr = $values->{$index}{'addr'};
        my $prefix = $values->{$index}{'prefix'};


        $desc=~s/\'/\\\'/s;
        $iface=~ tr/ \//./s;

        next if not AllowInterface($opts,$host,"$iface $desc",$stat,$speed);

        my $if_in='1.3.6.1.2.1.2.2.1.10';  # ifInOctets
        my $if_out='1.3.6.1.2.1.2.2.1.16'; # ifOutOctets
        my $command = 'CIBH::DS::' . $opts->{datastore}->{name} . '::OctetsAppend';
        if (defined($highspeed)) {
            $if_in="1.3.6.1.2.1.31.1.1.1.6"; # ifHCInOctets
            $if_out="1.3.6.1.2.1.31.1.1.1.10"; # ifHCOutOctets
            $speed=$highspeed * 1_000_000;
            $command = 'CIBH::DS::' . $opts->{datastore}->{name} .'::OctetsAppend64';
        }

        $config->{$host}->{"$iface.in"}={
            oid=>"$if_in.$index",
            command=>$command,
            desc=>$desc,
            iface=>$iface,
            addr=>$addr,
            prefix=>$prefix,
            max=>$speed,
            file=>"$iface.in",
        };

        $config->{$host}->{"$iface.out"}={
            oid=>"$if_out.$index",
            command=>$command,
            iface=>$iface,
            desc=>$desc,
            addr=>$addr,
            prefix=>$prefix,
            max=>$speed,
            file=>"$iface.out",
        };
    }
    return $config;
}

sub AllowInterface {
    my($opts,$rtr,$desc,$stat,$speed)=(@_);

    warn "Interface: $rtr $desc speed=$speed stat=$stat\n"
        if($opts->{debug});
    if ($opts->{accept} and not $desc=~/$opts->{accept}/) {
        warn "can not accept $rtr $desc\n" if ($opts->{debug});
        return 0;
    }

    if ($opts->{reject} and $desc=~/$opts->{reject}/) {
        warn "rejecting $rtr $desc\n" if($opts->{debug});
        return 0;
    }

    if ($opts->{noshutdown} and $stat!=1)  {
        warn "ignoring shutdown interface $rtr $desc\n" if ($opts->{debug});
        return 0;
    }

    if ($speed==0) {
        warn "speed is zero, reject $rtr $desc\n" if ($opts->{debug});
        return 0;
    }

    warn "Accepted interface\n" if ($opts->{debug});

    return 1;
}

Main;
